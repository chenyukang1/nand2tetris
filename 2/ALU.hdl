// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    Not16 (in=x, out=notx);
    Not16 (in=y, out=noty);
    Mux16 (a=x, b[0..15]=false, sel=zx, out=zxx);
    Not16 (in=zxx, out=notzxx);
    Mux16 (a=zxx, b=notzxx, sel=nx, out=realx);
    Mux16 (a=y, b[0..15]=false, sel=zy, out=zyy);
    Not16 (in=zyy, out=notzyy);
    Mux16 (a=zyy, b=notzyy, sel=ny, out=realy);
    Add16 (a=realx, b=realy, out=xAddy);
    And16 (a=realx, b=realy, out=xAndy);
    Mux16 (a=xAndy, b=xAddy, sel=f, out=res);
    Not16 (in=res, out=notres);
    Mux16 (a=res, b=notres, sel=no, out=out, out[0..7]=low8, out[8..15]=high8, out[15]=signal);
    Or8Way (in=low8, out=low8or);
    Or8Way (in=high8, out=high8or);
    Or (a=low8or, b=high8or, out=or16);
    Mux (a=true, b=false, sel=or16, out=zr);
    Mux (a=false, b=true, sel=signal, out=ng);
}
